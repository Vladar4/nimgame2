<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8"/>

    <title>Nimgame 2</title>

    <link rel="stylesheet" type="text/css" href="style.css" media="screen"/>
</head>
<body>

<noscript>This site needs JavaScript to work properly.</noscript>

<header>
</header>

<main>

    <aside>
    </aside>

    <article>
        <h2>TUTORIAL</h2>

<div class="section" id="introduction"><h3>Introduction</h3>

</div>


<div class="section" id="assets"><h3>Assets</h3>
    <p>
        For any basic game you'll need three basic kinds of assets:
    </p>
    <ul>
        <li>fonts</li>
        <li>graphics</li>
        <li>sounds</li>
    </ul>
    <p>
        So, the assets directory of the game will be structured as showed here:
    </p>
<pre>
    ./data/
      |
      |-- fnt/
      |-- gfx/
      |-- sfx/
</pre>
    <p>
        For rendering all text in the game we will use good old <a href="http://fixedsysexcelsior.com/" target="_blank">Fixedsys Excelsior</a> (at "data/fnt").
    </p>
    <p>
        We will need just two graphic assets here: the ball and the paddle (at &quot;data/gfx&quot;).
    </p>
    <p class="centered">
        <span><img src="images/tut101_bounce/ball.png"
             alt="Ball graphic asset"
             title="Ball"
        /></span><span>
        <img src="images/tut101_bounce/paddle.png"
             alt="Paddle graphic asset"
             title="Paddle"
        /></span>
    </p>
    <p>
        As for the sounds, we will not worry about it for now.
    </p>
</div>


<div class="section" id="starting"><h3>Starting to Code</h3>

    <p>
        All source code will be placed into the &quot;src&quot; directory. Let's start with a file for storing constants and variables that might be accessed from different points of the game.
    </p>

<h4>data.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    assets,
    scene,
    types,
  ]


const
  GameWidth* = 640
  GameHeight* = 360
  GameTitle* = "Nimgame 2 Bounce"


var
  titleScene*, mainScene*: Scene

</pre>

    <p>
        As you can see, we will have two game scenes: title and main one. We will create a souce file for each of them.
    </p>

<h4>title.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    nimgame,
    scene,
    types,
  ],
  data


type
  TitleScene = ref object of Scene


proc init*(scene: TitleScene) =
  init Scene(scene)


proc free*(scene: TitleScene) =
  discard


proc newTitleScene*(): TitleScene =
  new result, free
  init result


method event*(scene: TitleScene, event: Event) =
  if event.kind == KeyDown:
    game.scene = mainScene

</pre>

<h4>main.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    scene,
  ],
  data


type
  MainScene = ref object of Scene


proc init*(scene: MainScene) =
  init Scene(scene)


proc free*(scene: MainScene) =
  discard


proc newMainScene*(): MainScene =
  new result, free
  init result


method show*(scene: MainScene) =
  echo "Switched to MainScene"

</pre>

    <p>
        And, finally, we'll create a main source file:
    </p>

<h4>bounce.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    nimgame,
    settings,
    types,
  ],
  data,
  title,
  main


game = newGame()
if game.init(GameWidth, GameHeight, title = GameTitle, integerScale = true):

  # Init
  game.setResizable(true) # Window could be resized
  game.minSize = (GameWidth, GameHeight) # Minimal window size
  game.windowSize = (GameWidth * 2, GameHeight * 2) # Double scaling (1280x720)
  game.centrify() # Place window at the center of the screen

  # Create scenes
  titleScene = newTitleScene()
  mainScene = newMainScene()

  # Run
  game.scene = titleScene # Initial scene
  run game  # Let's go!

</pre>

    <p>
        During the process of development we will rebuild the game constantly, so let's create some basic scripts for that:
    </p>

<h4>build.debug.sh</h4>
<pre class="prettyprint">
    #!/bin/sh
    cd src
    nim c --out:../ng2bounce bounce.nim
    cd ..
</pre>

<h4>build.release.sh</h4>
<pre class="prettyprint">
    #!/bin/sh
    cd src
    nim c --out:../ng2bounce -d:release --opt:speed bounce.nim
    rm -rf nimcache
    cd ..
</pre>

    <p>
        Or, if you are working on Windows:
    </p>

<h4>build.debug.bat</h4>
<pre class="prettyprint">
    cd src
    nim c --out:..\ng2bounce.exe bounce.nim
    cd ..
</pre>

<h4>build.release.bat</h4>
<pre class="prettyprint">
    cd src
    nim c --out:..\ng2bounce -d:release --opt:speed --app:gui bounce.nim
    rmdir /s /q nimcache
    cd ..
</pre>

    <p>
        So, at this moment, our directory structure will look like this:
    </p>

<pre>
    ./ng2bounce/
      |
      |-- build.debug.sh
      |-- build.release.sh
      |
      |-- data/
      |     |
      |     |-- fnt/
      |     |     |
      |     |     |-- FSEX300.ttf
      |     |
      |     |-- gfx/
      |     |     |
      |     |     |-- ball.png
      |     |     |-- paddle.png
      |     |
      |     |-- sfx/
      |
      |-- src/
            |
            |-- bounce.nim
            |-- data.nim
            |-- main.nim
            |-- title.nim
</pre>

    <p>
        When we run our &quot;build.debug.sh&quot; script, we should get an executalbe &quot;ng2bounce&quot; that just shows a black screen and, when any keyboard key is pressed, outputs a &quot;Switched to MainScene&quot; message to the console.
    </p>

</div>


<div class="section" id="titlescreen"><h3>Title Screen</h3>

    <p>
        We'll start with populating the title screen. There will be two text graphics: the title of the game and a subtitle hint under it.
    </p>

    <p>
        To render any text we need to load our font first.
    </p>

<h4>data.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    ...
    font,
    ...
    truetypefont,
    ...
  ]

...

var
  ...
  defaultFont*, bigFont*: TrueTypeFont


proc loadData*() =
  defaultFont = newTrueTypeFont()
  if not defaultFont.load("data/fnt/FSEX300.ttf", 16):
    echo "ERROR: Can't load font"
  bigFont = newTrueTypeFont()
  if not bigFont.load("data/fnt/FSEX300.ttf", 32):
    echo "ERROR: Can't load font"


proc freeData*() =
  defaultFont.free()
  bigFont.free()

</pre>

    <p>
        We will call the loading procedure from our main file.
    </p>

<h4>bounce.nim</h4>
<pre class="prettyprint">
...

loadData() # Call it before any scene initialization

# Create scenes
...

</pre>

    <p>
        And finally, we will create our entities in TitleScene's <b>init()</b> procedure.
    </p>

<h4>title.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    entity,
    font,
    ...
    textgraphic,
    ...
  ],
  ...

proc init*(scene: TitleScene) =
  ...

  # Create a title text graphic with a big font
  let titleText = newTextGraphic(bigFont)
  titleText.setText GameTitle # Set the text to render

  let title = newEntity() # Create a title entity
  title.graphic = titleText # Assign the title text graphic
  title.centrify() # Set the origin point to the graphic's center
  title.pos = (GameWidth / 2, GameHeight / 3) # Set title position on screen
  scene.add title # Add title entity to the scene

  # Create a subtitle text graphic with a default font
  let subtitleText = newTextGraphic(defaultFont)
  subtitleText.setText "Press any key to start" # Set the text

  let subtitle = newEntity() # Create a subtitle entity
  subtitle.graphic = subtitleText # Assign the subtitle text graphic
  subtitle.centrify() # Set the origin point to the graphic's center
  subtitle.pos = game.size / 2 # Place to the center of the screen
  scene.add subtitle # Add subtitle entity to the scene

  ...

</pre>

    <p>
        After running the game we should see this screen:
    </p>

    <p>
        <img src="images/tut101_bounce/titlescene.png"
             alt="TitleScene screenshot"
             title="TitleScene screenshot"
        />
    </p>

</div>


<div class="section" id="loadingassets"><h3>Loading Assets</h3>

    <p>
        Though we only have two graphic assets now it is surelly an overkill to use an asset manager, but it is a good opportunity to give an example of its usage.
    </p>

<h4>data.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    ...
    texturegraphic,
    ...
  ]

...

var
  ...
  gfxData*:Assets[TextureGraphic]


proc loadData*() =
  ...

  gfxData = newAssets[TextureGraphic](
    "data/gfx",
    proc(file: string): TextureGraphic = newTextureGraphic(file))


proc freeData*() =
  ...
  for graphic in gfxData.values:
    graphic.free()

</pre>

    <p>
        As you can see, we pass two arguments to the asset manager constructor: a path to the assets, and a procedure to load them. The procedure recieves a file name and should return an object to be loaded in our collection.
    </p>

    <p>
        <b>Note:</b>Would you try to load any directory containing other types of files, the loading procedure will throw an error.
    </p>

</div>


<div class="section" id="paddles"><h3>Paddles</h3>

    <p>
        Let's start with creating paddle entity. This object will represent players in this game.
    </p>

<h4>paddle.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    assets,
    graphic,
    input,
    nimgame,
    entity,
  ],
  data


const
  speed = 250.0 # Speed (in pixels per second)


type
  PaddlePlacement* = enum
    ppLeft, ppRight

  PaddleControl* = enum
    pcPlayer1, pcPlayer2

  Paddle* = ref object of Entity
    control*: PaddleControl

</pre>

    <p>
        As you can see, two enums represent paddle position and how it will be controlled.
    </p>

<h4>paddle.nim</h4>
<pre class="prettyprint">
...

proc init*(paddle: Paddle, placement: PaddlePlacement, control: PaddleControl) =
  paddle.initEntity()
  paddle.graphic = gfxData["paddle"]
  paddle.centrify() # Set the center point offset

  # Set position
  paddle.pos = case placement:
    of ppLeft: (paddle.graphic.w.float, game.size.h / 2)
    of ppRight: (float(game.size.w - paddle.graphic.w), game.size.h / 2)

  paddle.control = control # Set control mode


proc newPaddle*(placement: PaddlePlacement, control: PaddleControl): Paddle =
  new result
  result.init(placement, control)

</pre>

    <p>
    As you see, for now we need to have only one param in our <b>Paddle</b> object&nbsp;â€” a control state, that we'll need in the <b>update()</b> method.
    </p>

<h4>paddle.nim</h4>
<pre class="prettyprint">
...

method update*(paddle: Paddle, elapsed: float) =
  var movement = speed * elapsed

  # Read input
  case paddle.control:
  # First player
  of pcPlayer1:
    if ScancodeQ.down: paddle.pos.y -= movement # Move up
    if ScancodeA.down: paddle.pos.y += movement # Move down
  # Second player
  of pcPlayer2:
    if ScancodeUp.down: paddle.pos.y -= movement    # Move up
    if ScancodeDown.down: paddle.pos.y += movement  # Move down

  # Check for the screen borders
  paddle.pos.y = clamp(
    paddle.pos.y,
    paddle.center.y,
    game.size.h.float - paddle.center.y)

</pre>

    <p>
        We calculate movement distance by multiplying the speed and time elapsed since previous update. Finally, don't forget to check for the screen borders.
    </p>

    <p>
        Now we just need to add two <b>Paddle</b> entities into the main scene.
    </p>

<h4>main.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    scene,
  ],
  data,
  paddle


type
  MainScene = ref object of Scene
    leftPaddle, rightPaddle: Paddle


proc init*(scene: MainScene) =
  init Scene(scene)

  # left paddle
  scene.leftPaddle = newPaddle(ppLeft, pcPlayer1)
  scene.add scene.leftPaddle

  # right paddle
  scene.rightPaddle = newPaddle(ppRight, pcPlayer2)
  scene.add scene.rightPaddle

...

</pre>

    <p>
        Now the main scene should look like this:
    </p>

    <p>
        <img src="images/tut101_bounce/paddles.png"
             alt="Paddles screenshot"
             title="Paddles screenshot"
        />
    </p>

</div>


<div class="section" id="colliders"><h3>Colliders</h3>

    <p>
        To detect collisions between the ball and paddles we need to init colliders for these entities. All collider counstructor accept a parent entity reference, center point offset, and collider dimensions that depend on a particular collider type. We will use a simple box collider for our paddles.
    </p>

<h4>paddle.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    ...
    collider,
    ...
  ],
  ...


proc init*(paddle: Paddle, placement: PaddlePlacement, control: PaddleControl) =
  ...

  # Collisions
  paddle.tags.add "paddle"
  paddle.collider = paddle.newBoxCollider((0.0, 0.0), paddle.graphic.dim)
  paddle.collider.tags.add "ball"

...

</pre>

    <p>
        The &quot;paddle&quot; and &quot;ball&quot; tags will be used to filter out the collisions.
    </p>

    <p>
        Now we define a <b>Ball</b> entity the same way, but using the circle collider instead of a box one, passing the circle radius to its constructor.
    </p>

<h4>ball.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    assets,
    collider,
    entity,
    graphic,
    nimgame,
    types,
    utils,
  ],
  data


type
  Ball* = ref object of Entity


proc reset*(ball: Ball) =
  ball.pos = game.size / 2  # place to the center of the screen


proc init*(ball: Ball) =
  ball.initEntity()
  ball.graphic = gfxData["ball"]
  ball.centrify() # Set the center point offset
  ball.reset()

  # Collisions
  ball.tags.add "ball"
  ball.collider = ball.newCircleCollider((0.0, 0.0), ball.graphic.dim.w / 2)
  ball.collider.tags.add "paddle"


proc newBall*(): Ball =
  new result
  result.init()

</pre>

    <p>
        To be sure that we have initialized colliders correctly we could use a <b>colliderOutline</b> boolean variable from the &quot;nimgame2/settings&quot; module.
    </p>


<h4>main.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    input,
    scene,
    settings,
  ],
  ball,
  ...


type
  MainScene = ref object of Scene
    ...
    ball: Ball


proc init*(scene: MainScene) =
  ...

  # ball
  scene.ball = newBall()
  scene.add scene.ball

...

method show*(scene: MainScene) =
  ...
  scene.ball.reset()

...

method update*(scene: MainScene, elapsed: float) =
  scene.updateScene(elapsed)

  if ScancodeF10.pressed: colliderOutline = not colliderOutline
  if ScancodeF11.pressed: showInfo = not showInfo

</pre>

    <p>
        Now we could press <kbd>F10</kbd> to toggle collider outlines. We also assigned the info panel toggle to a <kbd>F11</kbd> for good measure.
    </p>

    <p>
        <img src="images/tut101_bounce/colliders.png"
             alt="Collider outlines screenshot"
             title="Collider outlines screenshot"
        />
    </p>
</div>


<div class="section" id="bounce"><h3>Bounce</h3>

    <p>
        Now, as we have a ball entity, bounce logic could be implemented. The ball should bounce from top and bottom walls, and also from both paddles. Paddle bounces will increase ball's speed each time.
    </p>

<h4>ball.nim</h4>
<pre class="prettyprint">
...

const
  speed = 100.0 # Speed (in pixels per second)
  speedInc = 25.0 # Speed increase after each bounce

type
  Ball* = ref object of Entity
    radius: float


proc reset*(ball: Ball) =
  ball.pos = game.size / 2  # place to the center of the screen
  ball.vel.x = speed * randomSign().float
  ball.vel.y = speed * randomSign().float

...

method update*(ball: Ball, elapsed: float) =
  var movement = ball.vel * elapsed

  ball.pos += movement

  # Top and bottom walls collisions
  if ball.pos.y &lt; ball.radius or
     ball.pos.y &gt;= (game.size.h.float - ball.radius):
    ball.vel.y = -ball.vel.y


method onCollide*(ball: Ball, target: Entity) =
  if "paddle" in target.tags:
    # Check if the ball is in the front of a paddle
    if (ball.pos.y &gt;= target.pos.y - target.center.y) and
       (ball.pos.y &lt;= target.pos.y + target.center.y):

      ball.vel.x = -ball.vel.x  # change horizontal direction

      # Move the ball out of the collision zone
      if ball.pos.x &lt; game.size.w / 2:
        ball.pos.x = target.pos.x + target.center.x + ball.radius + 1
      else:
        ball.pos.x = target.pos.x - target.center.x - ball.radius - 1

      # increase speed
      ball.vel += (speedInc, speedInc) * ball.vel / abs(ball.vel)

</pre>
</div>


<!--

<div class="section" id=""><h3></h3>

<h4></h4>
<pre class="prettyprint">

</pre>
</div>

-->

    </article>

    <aside style="display: flex; border: 0;"></aside>
</main>

<footer>
</footer>

<script src="structure.js"></script>

<script>
    createSectionsList();
</script>

<script src="https://cdn.rawgit.com/google/code-prettify/master/loader/run_prettify.js"></script>

</body>
</html>

